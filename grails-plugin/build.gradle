sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencyManagement {
    imports {
        mavenBom "org.grails:grails-bom:$pluginGrailsVersion"
    }
    applyMavenExclusions false
}
configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if(details.requested.group == 'org.grails' && 
            details.requested.name.startsWith('grails-datastore') &&
            !details.requested.name.contains('mongodb') &&
            !details.requested.name.contains('bson')) {
            details.useVersion(datastoreVersion)
        } else if(details.requested.group == 'org.codehaus.groovy' && details.requested.name.startsWith('groovy')) {
            details.useVersion(groovyVersion)   
        } else if(details.requested.group == 'org.springframework') {
            details.useVersion(springVersion)
        }
    }
}
dependencies {
    compileOnly "org.grails:grails-core"
    compileOnly "org.grails:grails-bootstrap"

    compileOnly "org.spockframework:spock-core:$spockVersion", {
        exclude group: "junit", module: "junit-dep"
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
        exclude group: 'org.hamcrest', module: 'hamcrest-core'
    }

    api "org.grails:grails-datastore-web:$datastoreVersion"
    api "org.grails:grails-datastore-gorm-support:$datastoreVersion", {
        exclude group:'org.springframework', module:'spring-context'
        exclude group:'org.springframework', module:'spring-core'
        exclude group:'org.springframework', module:'spring-beans'
        exclude group:'org.springframework', module:'spring-tx'
        exclude group:'org.grails', module:'grails-bootstrap'
        exclude group:'org.codehaus.groovy', module:'groovy-all'
        exclude group:'org.grails', module:'grails-core'
        exclude group:'javax.transaction', module:'jta'
    }
    api project(":grails-datastore-gorm-mongodb-ext"), {
        exclude group:'org.grails', module:'grails-datastore-gorm-mongodb'
    }    
    api project(":grails-datastore-gorm-mongodb"), {
        exclude group:'org.springframework', module:'spring-context'
        exclude group:'org.springframework', module:'spring-core'
        exclude group:'org.springframework', module:'spring-beans'
        exclude group:'org.springframework', module:'spring-tx'
        exclude group:'org.grails', module:'grails-bootstrap'
        exclude group:'org.codehaus.groovy', module:'groovy-all'
        exclude group:'org.grails', module:'grails-core'
        exclude group:'javax.transaction', module:'jta'
    }
}
